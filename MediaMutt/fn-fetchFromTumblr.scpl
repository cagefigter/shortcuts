Comment "..:: fetchFromTumblr ::.."
GetVariable v:function
If Equals "fetchFromTumblr"
    @dbgAlert "Entered fetchFromTumblr with \(v:argv:url)"
    Dictionary {
        "photo"     : "(?m)og:image\"\\s+content=\"([^\"]+)",
        "photoset"  : "(?m)og:image\"\\s+content=\"([^\"]+)",
        "video"     : "(?m)og:video\"\\s+content=\"([^\"]+)",
    } -> mv:type_dict

    GetVariable v:argv:url
    GetContentsOfUrl
    SetName "src.txt" -> mv:src
    MatchText pattern="og:type\"\\s+content=\"([^\"]+)"
    GetGroupfromMatchedText get="Group At Index" groupIndex=1 -> mv:type

    GetVariable mv:type_dict
    GetDictionaryValue get=Value key=mv:type -> mv:pattern

    GetVariable mv:src
    MatchText pattern=mv:pattern
    RepeatWithEach
        GetGroupfromMatchedText get="Group At Index" groupIndex=1 -> mv:media_url
        @dbgAlert "url=[\(mv:media_url)]"
        If Contains "tumblr.com"
            GetVariable mv:media_url
            GetContentsOfUrl
        Otherwise
            ShowAlert (
                title=v:self,
                message="The url of the associated media is not hosted in tumblr.com. It will be share again with \(v:self) to attempt donwloading.",
                showCancelButton=false
            )
            GetVariable mv:media_url
            RunShortcut v:self false
        End
        AddToVariable v:downloaded_media
    End
    GetVariable v:downloaded_media
    ExitShortcut
End