GetVariable v:function
If Equals "showSettings"
    #ShowAlert title="Settings here" message=""

    Dictionary {
        ShowAlerts: {
            type        : "toggle",
            icon        : "toggle",
            caption     : "Show Alerts",
            description : "Messages as alerts instead of notifications.",
        },
        ViewBeforeSave: {
            type        : "toggle",
            icon        : "toggle",
            caption     : "Preview Before Saving",
            description : "Display the downloaded media before saving.",
        },
        DownloadImmediately: {
            type        : "toggle",
            icon        : "toggle",
            caption     : "Download Immediately",
            description : "Don't show menu when downloading.",
        },
        DownloadFolder: {
            type        : "text",
            icon        : "folder",
            caption     : "iCloud Drive Download Location",
            description : v:userOpts:DownloadFolder,
        },
        SaveTo: {
            type        : "list",
            icon        : "save",
            list        : ["Always Ask", "Files", "Photo Album"]
            caption     : "Save To",
            description : v:userOpts:SaveTo,
        },
        UpdateInterval: {
            type        : "number",
            icon        : "clock",
            caption     : "Update Check Frequency",
            description : "How often (in hours) to check for updates",
        },
    } -> v:opt_map


    # Get user options
    GetVariable v:userOpts
    GetDictionaryValue get="Value" key="sysOrder"
    RepeatwithEach

        # this will hold the option name
        SetVariable v:option_name

        # get mapping information for that option
        GetVariable v:opt_map
        GetDictionaryValue get="Value" key=v:option_name -> mv:mapped_item
        Count Items
        # show only if mapping is defined
        If "Is Greater Than" 0


            Text "\(mv:mapped_item.caption)" -> v:caption
            Text "\(mv:mapped_item.description)" -> v:optSubtitle
            GetVariable mv:icons
            GetDictionaryValue get="Value" key=mv:mapped_item.icon -> v:optIcon


            Text "\(mv:mapped_item.type)" -> v:mapped_item_type
            # on/off type
            If Equals "toggle"

                GetVariable v:userOpts
                GetDictionaryValue get="Value" key=v:option_name
                If Equals 1
                    Text "on"
                Otherwise
                    Text "off"
                End -> v:optState

                GetVariable mv:icons
                GetDictionaryValue get="Value" key="toggle-\(v:optState)" -> v:optIcon

                Nothing
            End

            // text type
            GetVariable v:mapped_item_type
            If Equals "text"
                Getvariable mv:mapped_item.description -> v:optState
                Nothing
            End

            GetVariable v:mapped_item_type
            If Equals "number"

                GetVariable v:userOpts
                GetDictionaryValue get="Value" key=v:option_name -> v:optState

                Nothing
            End
            GetVariable v:mapped_item_type
            If Equals "list"
                Getvariable mv:mapped_item.description -> v:optState
                Nothing
            End

            Dictionary {
                "caption" : v:caption,
                "subtitle": v:optSubtitle,
                "icon"    : v:optIcon,
                "value"   : {
                    option: v:option_name,
                    state : v:optState,
                    type  : v:mapped_item_type,
                    list  : mv:mapped_item.list
                }
            }
            AddToVariable v:options_list
        End
    End

    # add a reset settings item
    Dictionary {
        "caption" : "Reset Settings",
        "subtitle": "Reset settings to default.",
        "icon"    : mv:icons:history,
        "value"   : {
            option: "reset",
            type  : "reset"
        },
    }
    AddToVariable v:options_list

    # add an exit item
    Dictionary {
        "caption" : "Done",
        "subtitle": "Go back to the main menu",
        "icon"    : mv:icons:"arrow-alt-circle-left",
        "value"   : {
            option: "done",
            type  : "exit"
        },
    }
    AddToVariable v:options_list

    # display the menu
    Dictionary {
        fn:"chooseFromVcardMenu",
        caption: "Tap the option to change."
    }
    SetDictionaryValue key="menu_items" value=v:options_list
    RunShortcut shortcut=v:self false -> mv:chosen_item


    Dictionary {
        <bool> on   :false,
        <bool> off  :true,
    } -> mv:switcher


    GetVariable mv:chosen_item
    GetDictionaryValue get="Value" key="value"
    GetDictionaryfromInput -> v:chosen_value
    #@dbgViewWithJayson

    GetVariable v:chosen_value
    GetDictionaryValue get="Value" key="type" -> v:value_type

    @dbgAlert "value_type = \(v:value_type)"

    If Equals "toggle"

        GetVariable mv:switcher
        GetDictionaryValue get="Value" key=v:chosen_value.state -> v:newState

        GetVariable v:userOpts
        SetDictionaryValue key=v:chosen_value.option value=v:newState
        SetName v:opt:"storage.file"
        SaveFile a{
            service="iCloud Drive"
            askwheretosave=false
            destinationpath="\(v:opt:"storage.dir")/"
            overwriteiffileexists=true
        }
        Nothing

    End
    GetVariable v:value_type
    If Equals "text"
        AskforInput question=mv:chosen_item.caption defaultAnswer=v:chosen_value.state inputType="Text" -> v:newState

        GetVariable v:userOpts
        SetDictionaryValue key=v:chosen_value.option value=v:newState
        SetName v:opt:"storage.file"
        SaveFile a{
            service="iCloud Drive"
            askwheretosave=false
            destinationpath="\(v:opt:"storage.dir")/"
            overwriteiffileexists=true
        }

        GetVariable v:chosen_value.option
        If Equals "DownloadFolder"
            GetVariable v:newState
            If Equals v:userOpts.DownloadFolder
            Otherwise
                @call {fn:moveFiles, from:v:userOpts.DownloadFolder, to:v:newState}
                @call {fn:showMessage, message:"Downloaded files have been moved to /Shortcuts/\(v:newState)/."}
            End

        End

        Nothing

    End
    GetVariable v:value_type
    If Equals "list"

        GetVariable v:chosen_value.list
        SplitText separator="New Lines"
        ChoosefromList (
            prompt="\(mv:chosen_item.caption)",
            selectMultiple=false,
        ) -> v:newState

        GetVariable v:userOpts
        SetDictionaryValue key=v:chosen_value.option value=v:newState
        SetName v:opt:"storage.file"
        SaveFile a{
            service="iCloud Drive"
            askwheretosave=false
            destinationpath="\(v:opt:"storage.dir")/"
            overwriteiffileexists=true
        }

        Nothing

    End
    GetVariable v:value_type
    If Equals "number"

        # Get curret value
        GetVariable v:chosen_value
        GetDictionaryValue get="Value" key="state" -> v:number

        @dbgAlert "number = \(v:number)"

        # ask for a new value
        AskforInput (
            question=mv:chosen_item.caption,
            defaultAnswer="\(v:number)",
            inputType="Number",
        ) -> v:newState

        # Save the new value
        GetVariable v:userOpts
        SetDictionaryValue key=v:chosen_value.option value=v:newState
        SetName v:opt:"storage.file"
        SaveFile a{
            service="iCloud Drive"
            askwheretosave=false
            destinationpath="\(v:opt:"storage.dir")/"
            overwriteiffileexists=true
        }

        Nothing

    End
    GetVariable v:value_type
    If Equals "reset"
        ChooseFromMenu prompt="This will revert all your settings to default and delete all of the downloaded media." items=["Reset & Remove Media","Stay as is"]
        Case #Proceed
            GetFile (
                service="iCloud Drive",
                showDocumentPicker=false,
                filePath="\(v:userOpts.DownloadFolder)/",
                errorIfNotFound=false
            )
            DeleteFiles false

            GetVariable v:defaultOpts
            SetName v:opt:"storage.file"
            SaveFile a{
                service="iCloud Drive"
                askwheretosave=false
                destinationpath="\(v:opt:"storage.dir")/"
                overwriteiffileexists=true
            }
            @call {fn:showMessage, message:"All settings have been reset."}

        Case #Stay
            Nothing
        End
        Nothing
    End
    GetVariable v:value_type
    If Equals "exit"
        Nothing
    Otherwise
        @call {fn:showSettings}
    End

    ExitShortcut
End