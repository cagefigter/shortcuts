GetVariable v:function
If Equals "showSettings"
    #ShowAlert title="Settings here" message=""

    Dictionary {
        ShowAlerts: {
            type        : "toggle",
            icon        : "toggle",
            caption     : "Show Alerts",
            description : "Messages as alerts instead of notifications.",
        },
        ViewBeforeSave: {
            type        : "toggle",
            icon        : "toggle",
            caption     : "Preview Before Saving",
            description : "Display the downloaded media before saving.",
        },
        DownloadImmediately: {
            type        : "toggle",
            icon        : "toggle",
            caption     : "Download Immediately",
            description : "Don't show menu when downloading.",
        },
        DownloadFolder: {
            type        : "text",
            icon        : "folder",
            caption     : "iCloud Drive Download Location",
            description : v:userOpts:DownloadFolder,
        },
        SaveTo: {
            type        : "list",
            icon        : "save",
            list        : ["Always Ask", "Files", "Photo Album"]
            caption     : "Save To",
            description : v:userOpts:SaveTo,
        },
        UpdateInterval: {
            type        : "number",
            icon        : "clock",
            caption     : "Update Check Frequency",
            description : "How often (in hours) to check for updates",
        },
    } -> v:opt_map


    GetVariable v:userOpts.sysOrder
    #GetDictionaryValue get="All Keys"
    RepeatwithEach
        SetVariable v:option_name
        GetVariable v:opt_map
        GetDictionaryValue get="Value" key=v:option_name -> mv:mapped_item
        Count Items
        # show only if mapping is defined
        If "Is Greater Than" 0
            GetVariable mv:mapped_item.type
            If Equals "toggle"

                # get current value, translate to on/off
                GetVariable v:userOpts
                GetDictionaryValue get="Value" key=v:option_name
                If Equals 1
                    Text "on"
                Otherwise
                    Text "off"
                End -> v:optState

                GetVariable mv:icons
                GetDictionaryValue get="Value" key="toggle-\(v:optState)" -> v:optIcon

                Getvariable mv:mapped_item.description -> v:optSubtitle
                Nothing
            End
            GetVariable mv:mapped_item.type
            If Equals "text"
                Getvariable mv:mapped_item.description -> v:optSubtitle
                Getvariable mv:mapped_item.description -> v:optState

                GetVariable mv:icons
                GetDictionaryValue get="Value" key=mv:mapped_item.icon -> v:optIcon

                Nothing
            End
            GetVariable mv:mapped_item.type
            If Equals "number"
                Getvariable mv:mapped_item.description -> v:optSubtitle

                GetVariable v:userOpts
                GetDictionaryValue get="Value" key=v:option_name -> v:optState

                GetVariable mv:icons
                GetDictionaryValue get="Value" key=mv:mapped_item.icon -> v:optIcon

                Nothing
            End
            GetVariable mv:mapped_item.type
            If Equals "list"
                Getvariable mv:mapped_item.description -> v:optSubtitle
                Getvariable mv:mapped_item.description -> v:optState

                GetVariable mv:icons
                GetDictionaryValue get="Value" key=mv:mapped_item.icon -> v:optIcon

                Nothing
            End

            Dictionary {
                "caption" : mv:mapped_item.caption,
                "subtitle": v:optSubtitle,
                "icon"    : v:optIcon,
                "value"   : {
                    option: v:option_name,
                    state : v:optState,
                    type  : mv:mapped_item.type,
                    list  : mv:mapped_item.list
                }
            }
            AddToVariable v:options_list
        End
    End

    Dictionary {
        "caption" : "Reset Settings",
        "subtitle": "Reset settings to default.",
        "icon"    : mv:icons:history,
        "value"   : {
            option: "reset",
            type  : "reset"
        },
    }
    AddToVariable v:options_list


    Dictionary {
        "caption" : "Done",
        "subtitle": "Go back to the main menu",
        "icon"    : mv:icons:"arrow-alt-circle-left",
        "value"   : {
            option: "done",
            type  : "exit"
        },
    }
    AddToVariable v:options_list
    Dictionary {
        fn:"chooseFromVcardMenu",
        caption: "Tap the option to change."
    }
    SetDictionaryValue key="menu_items" value=v:options_list
    RunShortcut shortcut=v:self false -> mv:chosen_item

    Dictionary {
        <bool> on   :false,
        <bool> off  :true,
    } -> mv:switcher


    GetVariable mv:chosen_item.value
    GetDictionaryfromInput -> mv:value
    @dbgViewWithJayson

    GetVariable mv:value.type
    If Equals "toggle"

        GetVariable mv:switcher
        GetDictionaryValue get="Value" key=mv:value.state -> v:newState

        GetVariable v:userOpts
        SetDictionaryValue key=mv:value.option value=v:newState
        SetName v:opt:"storage.file"
        SaveFile a{
            service="iCloud Drive"
            askwheretosave=false
            destinationpath="\(v:opt:"storage.dir")/"
            overwriteiffileexists=true
        }
        Nothing

    End
    GetVariable mv:value.type
    If Equals "text"
        AskforInput question=mv:chosen_item.caption defaultAnswer=mv:value.state inputType="Text" -> v:newState

        GetVariable v:userOpts
        SetDictionaryValue key=mv:value.option value=v:newState
        SetName v:opt:"storage.file"
        SaveFile a{
            service="iCloud Drive"
            askwheretosave=false
            destinationpath="\(v:opt:"storage.dir")/"
            overwriteiffileexists=true
        }

        GetVariable mv:value.option
        If Equals "DownloadFolder"
            GetVariable v:newState
            If Equals v:userOpts.DownloadFolder
            Otherwise
                @call {fn:moveFiles, from:v:userOpts.DownloadFolder, to:v:newState}
                @call {fn:showMessage, message:"Downloaded files have been moved to /Shortcuts/\(v:newState)/."}
            End

        End

        Nothing

    End
    GetVariable mv:value.type
    If Equals "list"

        GetVariable mv:value.list
        SplitText separator="New Lines"
        ChoosefromList (
            prompt="\(mv:chosen_item.caption)",
            selectMultiple=false,
        ) -> v:newState

        GetVariable v:userOpts
        SetDictionaryValue key=mv:value.option value=v:newState
        SetName v:opt:"storage.file"
        SaveFile a{
            service="iCloud Drive"
            askwheretosave=false
            destinationpath="\(v:opt:"storage.dir")/"
            overwriteiffileexists=true
        }

        Nothing

    End
    GetVariable mv:value.type
    If Equals "number"
        AskforInput question=mv:chosen_item.caption defaultAnswer=mv:value.state inputType="Number" -> v:newState

        GetVariable v:userOpts
        SetDictionaryValue key=mv:value.option value=v:newState
        SetName v:opt:"storage.file"
        SaveFile a{
            service="iCloud Drive"
            askwheretosave=false
            destinationpath="\(v:opt:"storage.dir")/"
            overwriteiffileexists=true
        }

        Nothing

    End
    GetVariable mv:value.type
    If Equals "reset"
        ChooseFromMenu prompt="This will revert all your settings to default and delete all of the downloaded media." items=["Reset & Remove Media","Stay as is"]
        Case #Proceed
            CreateFolder service="iCloud Drive" path="\(v:userOpts.DownloadFolder)/"
            DeleteFiles false

            GetVariable v:defaultOpts
            SetName v:opt:"storage.file"
            SaveFile a{
                service="iCloud Drive"
                askwheretosave=false
                destinationpath="\(v:opt:"storage.dir")/"
                overwriteiffileexists=true
            }
            CreateFolder service="iCloud Drive" path="\(v:userOpts.DownloadFolder)/"
            @call {fn:showMessage, message:"All settings have been reset."}

        Case #Stay
            Nothing
        End
        Nothing
    End
    GetVariable mv:value.type
    If Equals "exit"
        Nothing
    Otherwise
        @call {fn:showSettings}
    End

    ExitShortcut
End