Comment `
..:: main function ::..
`
If Equals "main"
  @dbgAlert "Entered main"

  #
  # validate that there is an input
  #
  Number 0 -> v:hasUrl
  GetVariable v:argv:url
  Count Items
  If "Is Greater Than" 0
    GetVariable v:argv:url
    Count Characters
    If "Is Greater Than" 0
      Number 1 -> v:hasUrl
    End

  End

  Number 0 -> v:canDownload
  GetVariable v:hasUrl
  @dbgAlert "Has URL = \(v:hasUrl)"
  If Equals 0

    Text "" -> v:dl_menu_item
    GetVariable v:opt.TestMode
    If Equals 1
      Nothing
      ExitShortcut
    End

  Otherwise

    @call {fn:detectInput, url:v:argv:url}
    SetVariable v:input_type

    If Equals "UNSUPPORTED"


      Text -> v:dl_menu_item
      | { "caption":"Unsupported URL", "subtitle":"\(v:argv:url)", "value":"do-exit", "icon":"\(mv:icons.ban)" },

      GetVariable v:opt.TestMode
      If Equals 1
        Nothing
        ExitShortcut
      End


    Otherwise
      # if it's supported and is on test mode,
      # fetch the data and exit
      GetVariable v:opt.TestMode
      If Equals 1
        @call {fn:fetchMedia, type:v:input_type, url:v:argv:url}
        ExitShortcut
      End

      # otherwise, update the menu item
      Text -> v:dl_menu_item
      | { "caption":"Download", "subtitle":"\(v:argv:url)", "value":"do-fetch", "icon":"\(mv:icons.download)" },

      Number 1 -> v:canDownload

    End

  End

  # check for update
  Number 0 -> v:hasUpdate
  @call {
    fn          : getUpdates,
    name        : v:self,
    id          : v:opt:"updater.ID",
    version     : mv:meta:version,
    interval    : v:userOpts.UpdateInterval,
    lastupdate  : v:userOpts.sysLastUpdateCheck,
  }
  GetDictionaryfromInput
  SetVariable v:upd
  GetVariable v:upd.hasUpdate
  # if update is available, change the menu item
  If Equals "no"
    Text
    |{ "caption":"Check for Updates", "subtitle":"Check for new versions", "value":"do-cfu", "icon":"\(mv:icons.bullhorn)"},
  Otherwise
    Number 1 -> v:hasUpdate
    Text
    |{ "caption":"\(v:upd.updateMessage)", "subtitle":"\(v:upd.localVersion) -> \(v:upd.latestVersion)", "value":"do-cfu", "icon":"\(mv:icons.bullhorn)"},
  End -> mv:updateEntry


  # Standard Menu
  Text -> v:menu_txt
    |{
    |"fn": "chooseFromVcardMenu",
    |"caption": "\(v:self) v\(mv:meta.version)",
    |"menu_items": [
    | \(v:dl_menu_item)
    | { "caption":"Downloads", "subtitle":"Downloaded items in Files", "value":"show-downloads", "icon":"\(mv:icons.folder)" },
    | { "caption":"Settings", "subtitle":"Manage settings", "value":"show-settings", "icon":"\(mv:icons.cog)" },
    | { "caption":"Addons", "subtitle":"Manage addons", "value":"manage-addons", "icon":"\(mv:icons."puzzle-piece")" },
    | \(mv:updateEntry)
    | { "caption":"About", "subtitle":"About \(v:self)", "value":"do-about", "icon":"\(mv:icons."info-circle")"},
    | { "caption":"Exit", "subtitle":"Exit shortcut", "value":"do-exit", "icon":"\(mv:icons."arrow-alt-circle-left")"}
    | ]
    |}

  GetVariable v:userOpts.DownloadImmediately
  # show mini menu it DownloadImmediately is on and update is available
  If Equals 1

    GetVariable v:hasUpdate
    If Equals 1

      Text -> v:menu_txt
          |{
          |"fn": "chooseFromVcardMenu",
          |"caption": "\(v:self) v\(mv:meta.version)",
          |"menu_items": [
          | \(v:dl_menu_item)
          | \(mv:updateEntry)
          | { "caption":"Exit", "subtitle":"Exit shortcut", "value":"do-exit", "icon":"\(mv:icons."arrow-alt-circle-left")"}
          | ]
          |}

    Otherwise

      GetVariable v:canDownload
      If Equals 1
        @call {fn:fetchMedia, type:v:input_type, url:v:argv:url}
        ExitShortcut
      End
    End
  End

  @dbgAlert " menu_txt \n \(v:menu_txt)"
  GetVariable v:menu_txt
  #QuickLook
  #RunShortcut shortcut="View With Jayson" showWhileRunning=false
  GetDictionaryFromInput -> mv:menu
  @dbgAlert " menu_dict \n \(mv:menu)"

  RunShortcut shortcut=v:self false -> mv:chosen_item

  GetVariable mv:chosen_item:value
  If Equals "do-fetch"
    @call {fn:fetchMedia, type:v:input_type, url:v:argv:url}
    #@call{fn:main}
    ExitShortcut
  End
  If Equals "do-exit"
    ExitShortcut
  End
  If Equals "do-cfu"
    @call {
        fn          : checkForUpdate,
        name        : v:self,
        id          : v:opt:"updater.ID",
        version     : mv:meta:version,
        interval    : v:userOpts.UpdateInterval,
        lastUpdate  : v:userOpts.sysLastUpdateCheck,
    }
    Nothing
  End
  If Equals "show-settings"
    @call{fn:showSettings}
    @call{fn:main}
    Nothing
  End
  If Equals "manage-addons"
    @call{fn:manageAddons }
    @call{fn:main}
    Nothing
  End
  If Equals "show-downloads"
    @call{fn:showDownloads }
    @call{fn:main}
    Nothing
  End
  If Equals "do-about"
    Text -> mv:about
    |\(v:self) \(mv:meta:version)
    |Created by: \(mv:meta:author)
    |_____________________
    |
    | Youtube - Instagram - Reddit
    | Tumblr - Imgur - Gfycat
    | Streamable - More!
    |_____________________
    |
    |Credits
    |
    |** https://scpl.dev **
    |
    |Icons from: https://fontawesome.com/
    | courtesy of @atnbueno over at Discord
    |
    |Youtube Download: https://tyrrrz.me/Blog/Reverse-engineering-YouTube
    |
    |Audio/Video merging: http://www.fileconverto.com/add-music-to-video/
    ShowAlert title="" message=mv:about showCancelButton=false
    @call{fn:main}
    Nothing
  End
  @dbgAlert "Exited main"
  ExitShortcut
End
